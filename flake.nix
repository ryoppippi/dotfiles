{
  description = "ryoppippi's home-manager configuration";

  # Note: cachix configuration is defined in nix/cachix.nix
  # but nixConfig must be a literal set, so we inline it here
  nixConfig = {
    extra-substituters = [
      "https://cache.nixos.org"
      "https://cache.numtide.com"
      "https://devenv.cachix.org"
      "https://ryoppippi.cachix.org"
    ];
    extra-trusted-public-keys = [
      "cache.nixos.org-1:6NCHdD59X431o0gWypbMrAURkbJ16ZPMQFGspcDShjY="
      "niks3.numtide.com-1:DTx8wZduET09hRmMtKdQDxNNthLQETkc/yaX7M4qK0g="
      "devenv.cachix.org-1:w1cLUi8dv3hnoSPGAuibQv+f9TZLr6cv/Hm9XgU50cw="
      "ryoppippi.cachix.org-1:b2LbtWNvJeL/qb1B6TYOMK+apaCps4SCbzlPRfSQIms="
    ];
  };

  inputs = {
    nixpkgs.url = "github:nixos/nixpkgs?ref=nixos-unstable";

    blueprint = {
      url = "github:numtide/blueprint";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    nix-darwin = {
      url = "github:LnL7/nix-darwin";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    llm-agents.url = "github:numtide/llm-agents.nix";

    claude-code-overlay = {
      url = "github:ryoppippi/claude-code-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    treefmt-nix = {
      url = "github:numtide/treefmt-nix";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    gh-nippou = {
      url = "github:ryoppippi/gh-nippou";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    brew-nix = {
      url = "github:BatteredBunny/brew-nix";
      inputs.brew-api.follows = "brew-api";
      inputs.nix-darwin.follows = "nix-darwin";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    brew-api = {
      url = "github:BatteredBunny/brew-api";
      flake = false;
    };

    rs-arto = {
      url = "github:lambdalisue/rs-arto";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    fish-na = {
      url = "github:ryoppippi/fish-na";
      flake = false;
    };

    gh-graph = {
      url = "github:kawarimidoll/gh-graph";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    nix-index-database = {
      url = "github:nix-community/nix-index-database";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    inputs:
    let
      username = "ryoppippi";
      systems = [
        "aarch64-darwin"
        "x86_64-linux"
        "aarch64-linux"
      ];

      # Helper to generate attrs for all systems
      forAllSystems = f: inputs.nixpkgs.lib.genAttrs systems f;

      # Create pkgs for a given system
      mkPkgs = system: import inputs.nixpkgs {
        inherit system;
        config.allowUnfree = true;
      };
    in
    # Blueprint generates: darwinConfigurations, legacyPackages.*.homeConfigurations
    inputs.blueprint {
      inherit inputs systems;
      nixpkgs.config.allowUnfree = true;
    }
    // {
      # Custom apps (not auto-generated by blueprint)
      apps = forAllSystems (
        system:
        let
          pkgs = mkPkgs system;
          isDarwin = pkgs.stdenv.isDarwin;
          homedir = if isDarwin then "/Users/${username}" else "/home/${username}";
          hostname =
            if isDarwin then
              username
            else if system == "x86_64-linux" then
              "linux-x86_64"
            else
              "linux-aarch64";
        in
        {
          nvim-restore = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "nvim-restore" ''
                : "''${DOTFILES_DIR:=${homedir}/ghq/github.com/ryoppippi/dotfiles}"
                if [ ! -d "$DOTFILES_DIR" ]; then
                  DOTFILES_DIR="$(pwd)"
                fi
                exec ${pkgs.bash}/bin/bash \
                  ${./nix/modules/home/programs/neovim/check.sh} \
                  "$DOTFILES_DIR/nvim" \
                  "$HOME/.local/share/nvim/lazy" \
                  ${pkgs.neovim}/bin/nvim
              ''
            );
          };

          build = {
            type = "app";
            program = toString (
              pkgs.writeShellScript (if isDarwin then "darwin-build" else "home-manager-build") ''
                set -e
                echo "Building ${if isDarwin then "darwin" else "Home Manager"} configuration..."
                nix build .#${
                  if isDarwin then
                    "darwinConfigurations.${hostname}.system"
                  else
                    "legacyPackages.${system}.homeConfigurations.\"${username}@${hostname}\".activationPackage"
                }
                echo "Build successful! Run 'nix run .#switch' to apply."
              ''
            );
          };

          switch = {
            type = "app";
            program = toString (
              pkgs.writeShellScript (if isDarwin then "darwin-switch" else "home-manager-switch") ''
                set -e
                echo "Building and switching to ${if isDarwin then "darwin" else "Home Manager"} configuration..."
                ${
                  if isDarwin then
                    "sudo nix run nix-darwin -- switch --flake .#${hostname}"
                  else
                    "nix run nixpkgs#home-manager -- switch --flake .#${username}@${hostname}"
                }
              ''
            );
          };

          update = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "flake-update" ''
                set -e
                echo "Updating flake.lock..."
                nix flake update
                echo "Done! Run 'nix run .#switch' to apply changes."
              ''
            );
          };

          update-ai-tools = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "update-ai-tools" ''
                set -e
                echo "Updating AI tools inputs..."
                nix flake update llm-agents
                echo "Done! Run 'nix run .#switch' to apply changes."
              ''
            );
          };

          fmt = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "treefmt-wrapper" ''
                exec nix fmt "$@"
              ''
            );
          };

          update-node-packages = {
            type = "app";
            program = toString (
              pkgs.writeShellScript "update-node-packages" ''
                set -e
                exec ${pkgs.bash}/bin/bash nix/packages/node/update.sh "$@"
              ''
            );
          };
        }
      );

      # Formatter configuration (treefmt-nix)
      formatter = forAllSystems (
        system:
        let
          pkgs = mkPkgs system;
          treefmtEval = inputs.treefmt-nix.lib.evalModule pkgs {
            projectRootFile = "flake.nix";
            programs = {
              nixfmt = {
                enable = true;
                package = pkgs.nixfmt-rfc-style;
              };
              stylua.enable = true;
              shfmt.enable = true;
            };
            settings = {
              global.excludes = [
                ".git/**"
                "*.lock"
              ];
              formatter = {
                oxfmt = {
                  command = "${pkgs.oxfmt}/bin/oxfmt";
                  options = [ "--no-error-on-unmatched-pattern" ];
                  includes = [ "*" ];
                  excludes = [
                    "nvim/template/**"
                    "nvim/lazy-lock.json"
                  ];
                };
                gitleaks = {
                  command = "${pkgs.gitleaks}/bin/gitleaks";
                  options = [
                    "detect"
                    "--no-git"
                    "--exit-code"
                    "0"
                  ];
                  includes = [ "*" ];
                  excludes = [
                    "*.png"
                    "*.jpg"
                    "*.jpeg"
                    "*.gif"
                    "*.ico"
                    "*.pdf"
                    "*.woff"
                    "*.woff2"
                    "*.ttf"
                    "*.eot"
                    "node_modules/**"
                    ".direnv/**"
                    "nix/packages/node/**/package-lock.json"
                  ];
                };
                renovate-validator = {
                  command = "${pkgs.renovate}/bin/renovate-config-validator";
                  options = [ "--strict" ];
                  includes = [
                    ".github/renovate.json5"
                  ];
                };
                fish-indent = {
                  command = "${pkgs.fish}/bin/fish_indent";
                  options = [ "--write" ];
                  includes = [ "*.fish" ];
                };
              };
            };
          };
        in
        treefmtEval.config.build.wrapper
      );

      # DevShells
      devShells = forAllSystems (
        system:
        let
          pkgs = mkPkgs system;
        in
        {
          default = pkgs.mkShell {
            shellHook = ''
              echo "Dotfiles development shell"
            '';
          };
        }
      );
    };
}
